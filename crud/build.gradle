plugins {
    id 'java'
    id 'jacoco'
    id 'application'
}

group = 'com.crud'
version = '1.0-SNAPSHOT'
sourceCompatibility = '17'
targetCompatibility = '17'

repositories {
    mavenCentral()
}

dependencies {
    // Web Server e JSON
    implementation 'io.javalin:javalin:5.6.3'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.16.1'
    implementation 'org.slf4j:slf4j-simple:2.0.9'
    
    // Testes
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.1'
    
    // Jqwik para testes de propriedades
    testImplementation 'net.jqwik:jqwik-api:1.8.0'
    testRuntimeOnly 'net.jqwik:jqwik-engine:1.8.0'
    
    // Selenium para testes de interface
    testImplementation 'org.seleniumhq.selenium:selenium-java:4.16.1'
    testImplementation 'io.github.bonigarcia:webdrivermanager:5.6.3'
    
    // Mockito para testes do Controller
    testImplementation 'org.mockito:mockito-inline:4.11.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:4.11.0'
}

application {
    mainClass = 'crud.Main'
}

test {
    useJUnitPlatform {
        includeEngines 'junit-jupiter', 'jqwik'
    }
    
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
    
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.11"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
        csv.required = true
    }
    
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                'crud/controller/*',
                'crud/Main.class',
                'crud/Main$*.class'
            ])
        }))
    }
}

jacocoTestCoverageVerification {
    dependsOn jacocoTestReport
    
    violationRules {
        rule {
            element = 'BUNDLE'
            limit {
                counter = 'INSTRUCTION'
                value = 'COVEREDRATIO'
                minimum = 0.80
            }
        }
    }
    
    afterEvaluate {
        classDirectories.setFrom(files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                'crud/controller/*',
                'crud/Main.class',
                'crud/Main$*.class'
            ])
        }))
    }
}

check.dependsOn jacocoTestCoverageVerification

